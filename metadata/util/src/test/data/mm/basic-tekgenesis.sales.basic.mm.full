package tekgenesis.sales.basic;

entity Cart "Cart"
    primary_key id
{
    id    "Id"    : Int;
    items "Items" : entity CartItem* {
        product  "Product"  : Product;
        quantity "Quantity" : Int;
    };
}

form CartForm "Cart"
    entity Cart
{
    id       "Id"    : id, text_field;
    items    "Items" : items, table {
        product  "Product"     : product, suggest_box, on_change updateUnitPrice;
        unit     "Unit Price"  : Decimal(10, 2), text_field, disable;
        quantity "Quantity"    : quantity, text_field;
        total    "Total Price" : Decimal(10, 2), text_field, is unit * quantity, disable;
    };
    subtotal "$"     : Decimal(10, 2), text_field, optional, is sum(total), disable, style "pull-right aggregator";
    horizontal, style "form-actions" {
        button(save);
        button(cancel);
        button(delete), style "pull-right";
    };
}


entity Category "Category"
    primary_key id
    described_by descr
    search_by name, descr, id
{
    id       "Id #"        : Int, check id > 0 : "Must be positive";
    name     "Name"        : String(30);
    descr    "Description" : String(120), default name;
    products "Products"    : Product* using category;
}

form CategoryForm "Category"
    entity Category
{
    editMode               : Boolean, internal;
    id       "Id #"        : id, text_field, check (id > 0 : "Must be positive", id > 0 : "Must be positive");
    name     "Name"        : name, text_field(30);
    descr    "Description" : descr, text_area(10, 80), default name;
    products "Products"    : products, table, hide when editMode == false {
        productId   "Id"          : productId, text_field(8);
        model       "Model No."   : model, text_field(30);
        description "Description" : description, text_field(100), optional;
        price       "Price"       : price, text_field;
        state       "State"       : state, combo_box;
    };
    horizontal, style "form-actions" {
        button(save);
        button(cancel);
        button(delete), style "pull-right";
    };
}

entity Customer "Customer"
    primary_key documentType, documentId
    described_by firstName, lastName
    search_by documentId, firstName, lastName
    unique nick(nickname)
    index last(lastName, firstName)
{
    documentType            : DocType;
    documentId   "Document" : Decimal(10);
    firstName               : String(50);
    lastName                : String(50);
    nickname     "Username" : String(50);
    sex                     : Sex;
}

form CustomerForm "Customer"
    entity Customer
{
    documentType "Document Type" : documentType, combo_box;
    documentId   "Document"      : documentId, text_field;
    firstName    "First name"    : firstName, text_field(50);
    lastName     "Last name"     : lastName, text_field(50);
    nickname     "Nickname"      : nickname, text_field(50), default (substring(firstName, 0, 1) + "") + lastName;
    sex          "Sex"           : sex, combo_box;
    horizontal, style "form-actions" {
        button(save);
        button(cancel);
        button(delete), style "pull-right";
    };
}

form CustomerReviewForm "Customer Review"
    entity Customer
{
    documentType "Document Type" : String, text_field, disable;
    documentId   "Document Id"   : documentId, text_field, disable;
    firstName    "First name"    : firstName, text_field(50), disable;
    lastName     "Last name"     : lastName, text_field(50), disable;
    nickname     "Nickname"      : nickname, text_field(50), default (substring(firstName, 0, 1) + "") + lastName, disable;
    sex          "Sex"           : String, text_field, disable;
}

open enum DocType "Document Type"
with {
    local    : Boolean;
}
{
    DNI  : "DNI", true;
    PASS : "Passport", false;
}

form DocTypeForm "Doc Type Form"
    enum DocType
{
    values    : DocType, table(10) {
        id    "Id"    : String, text_field, unique;
        label "Label" : String, text_field;
    };
    horizontal, style "form-actions" {
        button(save);
        button(cancel);
    };
}


entity Invoice "Invoice"
    primary_key id
{
    id          "Id"       : Int;
    invoiceDate "Date"     : Date;
    customer    "Customer" : Customer;
    items       "Items"    : entity Item* {
        product  "Product"  : Product;
        quantity "Quantity" : Int;
        discount "Discount" : Int;
    };
    payments    "Payments" : entity Payment* {
        payment "Payment" : PaymentType;
        amount  "Amount"  : Decimal(10, 2);
    };
}

form InvoiceForm "Invoice Form"
    entity Invoice
{
    id          "Id"       : id, text_field;
    invoiceDate "Date"     : invoiceDate, date_box;
    customer    "Customer" : customer, suggest_box, on_change updateCustomer, on_new createCustomer;
    disc                   : Int, internal, default 10;
    items       "Items"    : items, table,
                             row_style (((discount * 100) / (unit * quantity)) > disc) ? "red" : ((((discount * 100) / (unit * quantity)) >= (disc / 2)) && (((discount * 100) / (unit * quantity)) <= disc)) ? "yellow" : "" {
        product  "Product"     : product, suggest_box, on_change updateUnitPrice;
        unit     "Unit Price"  : Decimal(10, 2), text_field, disable;
        quantity "Quantity"    : quantity, text_field;
        discount "Discount"    : discount, text_field, default 0;
        total    "Total Price" : Decimal(10, 2), text_field, is (unit * quantity) - discount, disable;
    };
    subtotal    "$"        : Decimal(10, 2), text_field, optional, is sum(total), disable, style "pull-right aggregator";
    payments    "Payments" : payments, table {
        type    "Type"    : PaymentOption, suggest_box, on_change updatePayments;
        payment "Payment" : payment, combo_box;
        amount  "Amount"  : amount, text_field;
    };
    received    "$"        : Decimal(10, 2), text_field, optional, is sum(amount), disable, style "pull-right aggregator",
                             check ((subtotal - received) >= 0.0 : inline "Exceeded payments", (subtotal - received) <= 0.0 : inline "Missing payments");
    horizontal, style "form-actions" {
        button(save);
        button(cancel);
        button(delete), style "pull-right";
    };
}

form InvoiceReviewForm "Invoice Review" on_load load{
    invoices    : String, table, static {
        id       "Id"       : Int, text_field, disable;
        items    "Items"    : sub_form(ItemsForm), display count + " Items";
        payments "Payments" : sub_form(PaymentsForm), display "$ " + received;
        customer "Customer" : sub_form(CustomerReviewForm), display (lastName + ", ") + firstName;
        date     "Date"     : date_box, disable;
    };
}


form ItemsForm "Items" {
    count        : Int, internal, default 0;
    items        : String, table, static {
        product  "Product"    : String(25), text_field(25), disable;
        unit     "Unit Price" : Decimal(10, 2), text_field, disable;
        quantity "Quantity"   : Int, text_field, disable;
        total    "Total"      : Decimal(10, 2), text_field, is unit * quantity, disable;
    };
    subtotal "$" : Decimal(10, 2), text_field, optional, is sum(total), disable, style "pull-right aggregator";
}

enum MailDigest "Mail Digest" {
    DAILY   : "Daily";
    WEEKLY  : "Weekly";
    SUMMARY : "Summary";
    NONE    : "None";
}


open enum PaymentOption "Payment Option" {
    CASH   : "Cash";
    DEBIT  : "Debit";
    CREDIT : "Credit";
    CHECK  : "Check";
}

form PaymentOptionForm "Payment Option Form"
    enum PaymentOption
{
    values    : PaymentOption, table(10) {
        id    "Id"    : String, text_field, unique;
        label "Label" : String, text_field;
    };
    horizontal, style "form-actions" {
        button(save);
        button(cancel);
    };
}

entity PaymentType "Payment Type"
    described_by subtype
    search_by type, subtype, description
    index type(type)
{
    type        "Type"        : PaymentOption;
    subtype     "SubType"     : String(30);
    description "Description" : String(160), default subtype;
}

form PaymentTypeForm "Payment Type Form"
    entity PaymentType
{
    id          "Id"          : id, internal;
    type        "Type"        : type, combo_box;
    subtype     "SubType"     : subtype, text_field(30);
    description "Description" : description, text_area, default subtype;
    horizontal, style "form-actions" {
        button(save);
        button(cancel);
        button(delete), style "pull-right";
    };
}

form PaymentsForm "Payments" {
    payments     : String, table, static {
        type    "Type"    : String(25), text_field(25), disable;
        payment "Payment" : String(25), text_field(25), disable;
        amount  "Amount"  : Decimal(10, 2), text_field, disable;
    };
    received "$" : Decimal(10, 2), text_field, optional, is sum(amount), disable, style "pull-right aggregator";
}

entity Preferences "Customer Preferences"
    described_by customer
    unique customer(customer)
{
    customer "Customer" : Customer;
    mail     "Email"    : String(60);
    twitter             : String(60);
    digest              : MailDigest;
}

form PreferencesForm "Customer Preferences"
    entity Preferences
{
    id       "Id"       : id, internal;
    customer "Customer" : customer, suggest_box, disable;
    mail     "Email"    : mail, text_field(25);
    digest   "Digest"   : digest, combo_box;
    twitter  "Twitter"  : twitter, text_field(25), placeholder "@garbarino", on_change checkUser;
    image               : image, optional, hide when twitter == "";
    horizontal, style "form-actions" {
        button(save);
         "Skip" : button(cancel);
        button(delete), style "pull-right";
    };
}

entity Product "Product"
    primary_key productId
    described_by description
    search_by model, description, productId
{
    productId   "Id"          : String(8), mask "XX-XXXX-XX";
    model       "Model No."   : String(30);
    description "Description" : String(100), optional;
    price       "Price"       : Decimal(10, 2);
    state       "State"       : State;
    category    "Category"    : Category using products;
    images                    : entity Image* described_by imageId {
        imageId    : String(100);
    };
}

form ProductForm "Product Form"
    entity Product
{
    productId   "Id"          : productId, text_field(8);
    model       "Model No."   : model, text_field(30);
    description "Description" : description, text_area, optional, default model;
    price       "Price"       : price, text_field;
    state       "State"       : state, combo_box;
    category    "Category"    : category, suggest_box, on_new createCategory;
    gallery     "Images"      : gallery(image), optional, multiple, file_type "image";
    horizontal, style "form-actions" {
        button(save);
        button(cancel);
        button(delete), style "pull-right";
    };
}

form Products "Products" on_load load{
    products    : String, table {
        productId  "Product Id"  : String, text_field, disable, style "small";
        model      "Model"       : String, text_field, disable;
        category   "Cateogry"    : String, text_field, disable;
        descrption "Description" : String, text_field, disable;
        active     "Active?"     : check_box, disable, style "small";
        price      "Price"       : String, text_field, disable, style "small";
        image      "Image"       : image;
    };
}

enum Sex {
    F : "Female";
    M : "Male";
}

enum State {
    ACTIVE       : "Active";
    DISCONTINUED : "Discontinued";
}
