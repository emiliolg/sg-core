import org.apache.tools.ant.filters.*

configurations {
    all*.exclude group: 'org.ow2.asm', module: 'asm'
}

dependencies {
    implementation project(':db:entity')
    implementation project(':db:base')
    implementation project(':db:introspect')
    implementation project(':parser:common')
    implementation project(':parser:metamodel')
    implementation project(':plugin:common')
    implementation project(':plugin:jps')
    implementation project(':codegen:metamodel')
    implementation project(':codegen:base')
    implementation project(':metadata:util')
    implementation project(':metadata:form')
    implementation project(':metadata:type')
    implementation project(':metadata:entity')
    implementation project(':metadata:workflow')
    implementation project(':metadata:handler')
    implementation project(':metadata:builder')
    implementation project(':runtime:persistence')
//    implementation project(':doc')
    runtimeOnly project(':projects:authorization')
    implementation bingTranslate
    provided fileTree(dir: '../ideaLibraries', include: '**/*.jar', exclude: '**/junit-*.jar')
    implementation fileTree(dir: '../lib', include: '**/*.jar')
    if(Boolean.getBoolean("teamcity.inspections"))
        implementation fileTree(dir: '../ideaLibraries', include: '**/*.jar', exclude: '**/junit-*.jar')
    testImplementation project(':metadata:type')
    testRuntimeOnly files("${System.properties['java.home']}/../lib/tools.jar")
}

test {
    debug = true;
}

test.doFirst {
    systemProperty 'idea.load.plugins.id', 'tekgenesis.mm'
    systemProperty 'idea.plugins.path', new File("target/plugin/mm/classes").getAbsolutePath()
    systemProperty 'idea.system.path', new File("../target/plugin/mm").getAbsolutePath()
    systemProperty 'idea.config.path', new File("../target/plugin/mm").getAbsolutePath()
    systemProperty 'idea.home.path', new File("../target/plugin/mm").getAbsolutePath()
    enableAssertions =  false
}

idea {
    module {
        iml {
            withXml { provider ->
                provider.node.@type = "PLUGIN_MODULE"
                def node = provider.node.component.find { it.@name == 'DevKit.ModuleBuildProperties' }
                if (!node) {
                    provider.node.appendNode 'component' , [name: 'DevKit.ModuleBuildProperties',url:'file://$MODULE_DIR$/src/main/resources/META-INF/plugin.xml']
                }

                def jdkEntry = provider.node.component.orderEntry.find { it.@type == 'inheritedJdk'}

                if(jdkEntry) {
                   jdkEntry.@type = 'jdk'
                    if(Boolean.getBoolean("teamcity.inspections"))
                            jdkEntry.@jdkName = '1.8'
                    else
                            jdkEntry.@jdkName = 'IDEA SDK'
                   jdkEntry.@jdkType = 'IDEA JDK'
               }
            }
        }
    }
}

def deps = project.configurations.runtimeClasspath.files.findAll { f -> !f.absolutePath.contains("ideaLibraries")}

def classpath = ""

project.configurations.compileClasspath.files.each {
    classpath += it.getName()+";";
}

task('installDist', type: Copy) {
            from { jar.archivePath }
            from { deps }
            into new File(buildDir,'install/mm-plugin/lib')
}

task copyPluginXml(type: Copy) {

    from new File(project.projectDir,  'src/main/resources')
    include 'META-INF/plugin.xml'
    into new File(project.buildDir, 'classes/main')
    filter(ReplaceTokens,
            tokens: [BUILD_VERSION: buildVersion, BUILD_NUMBER: buildNumber, CLASSPATH: classpath])
}

installDist.dependsOn(jar)

jar.dependsOn(copyPluginXml)
